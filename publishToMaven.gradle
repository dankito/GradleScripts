
if (generatePublishingTaskForProject()) {
    def groupName = project.findProperty("group") ?: rootProject.findProperty("group") ?: project.group ?: rootProject.group
    def artifactName = project.findProperty("artifactName") ?: rootProject.findProperty("artifactName") ?: project.name ?: rootProject.name
    def projectVersion = rootProject.findProperty("version") ?: project.version ?: rootProject.version

    def projectDescription = project.findProperty("projectDescription") ?: rootProject.findProperty("projectDescription")
    def sourceCodeRepositoryBaseUrl = project.findProperty("sourceCodeRepositoryBaseUrl") ?: rootProject.findProperty("sourceCodeRepositoryBaseUrl")

    def licenseName = project.findProperty("licenseName") ?: rootProject.findProperty("licenseName") ?: "The Apache License, Version 2.0"
    def licenseUrl = project.findProperty("licenseUrl") ?: rootProject.findProperty("licenseUrl") ?: "http://www.apache.org/licenses/LICENSE-2.0.txt"

    def developerId = project.findProperty("developerId") ?: rootProject.findProperty("developerId") ?: "dankito"
    def developerName = project.findProperty("developerName") ?: rootProject.findProperty("developerName") ?: "Christian Dankl"
    def developerMail = project.findProperty("developerMail") ?: rootProject.findProperty("developerMail") ?: "maven@dankito.net"

    def packageGroup = project.findProperty("packageGroup") ?: rootProject.findProperty("packageGroup") ?: "net.dankito"
    
    configurePublishingTaskForProject(groupName, artifactName, projectVersion, projectDescription,
            sourceCodeRepositoryBaseUrl, licenseName, licenseUrl, developerId, developerName, developerMail, packageGroup)
}


def generatePublishingTaskForProject() {
    // don't apply maven-publish plugin on Android or Java application projects
    return plugins.findPlugin("com.android.application") == null &&
            plugins.findPlugin("application") == null
}


def configurePublishingTaskForProject(groupName, artifactName, projectVersion, projectDescription, sourceCodeRepositoryBaseUrl, licenseName, licenseUrl,
                                      developerId, developerName, developerMail, packageGroupName) {

    def isAndroidProject = project.hasProperty("android") // = isAndroidProject(project)

    if (isAndroidProject) {
        apply plugin: "com.android.library"
    }
    else {
        apply plugin: "java-library"
    }

    apply plugin: "maven-publish"
    apply plugin: "signing"


    def sonatypeUsername = rootProject.findProperty("ossrhUsername") // TODO: rename to sonatypeUsername
    def sonatypePassword = rootProject.findProperty("ossrhPassword") // TODO: rename to sonatypePassword

    def versionMajor = gradle.gradleVersion.substring(0, gradle.gradleVersion.indexOf('.'))
    if (Integer.parseInt(versionMajor) >= 6) { // is only available for Gradle versions 6 and above
        if (isAndroidProject == false) {
            java {
                withJavadocJar()
                withSourcesJar()
            }
        }
    }

    artifacts {
        if (isAndroidProject == false) {
            archives jar

            if (gradle.gradleVersion.startsWith("6.")) { // is only available for Gradle versions 6 and above
                archives sourcesJar
                archives javadocJar
            }
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                // artifact can be set with ext.artifactName in each sub project
                artifactId = artifactName

                if (isAndroidProject == false) {
                    from components.java
                }
//                else { // TODO: make this work, may we then can omit custom Android configuration
//                    from components.release
//                }

                pom {
                    name = artifactName
                    groupId = groupName
                    artifactId = artifactName
                    version = projectVersion
                    description = projectDescription
                    url = "https://$sourceCodeRepositoryBaseUrl"

                    scm {
                        connection = "scm:git:git://${sourceCodeRepositoryBaseUrl}.git"
                        developerConnection = "scm:git:ssh://${sourceCodeRepositoryBaseUrl}.git"
                        url = "https://$sourceCodeRepositoryBaseUrl"
                    }

                    developers {
                        developer {
                            id = developerId
                            name = developerName
                            email = developerMail
                        }
                    }

                    licenses {
                        license {
                            name = licenseName
                            url = licenseUrl
                        }
                    }
                }
            }
        }

        repositories {
            maven {
                def newRepo = project.findProperty("useNewSonatypeRepo") ?: rootProject.findProperty("useNewSonatypeRepo")  ?: false
                def releasesRepoUrl = newRepo ? "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                        : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = newRepo ? "https://s01.oss.sonatype.org/content/repositories/snapshots"
                        : "https://oss.sonatype.org/content/repositories/snapshots/"
                url = version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl

                if (sonatypeUsername != null && sonatypePassword != null) {
                    credentials {
                        username = sonatypeUsername
                        password = sonatypePassword
                    }
                }
            }
        }
    }

    if (sonatypeUsername != null && sonatypePassword != null) {
        signing {
            sign publishing.publications.mavenJava
        }
    }


    if (isAndroidProject == false) {
        javadoc {
            if(JavaVersion.current().isJava9Compatible()) {
                options.addBooleanOption("html5", true)
            }
        }
    }
    
    rootProject.pluginManager.withPlugin("io.codearte.nexus-staging") {
        nexusStaging {
            packageGroup = packageGroupName
            username = sonatypeUsername
            password = sonatypePassword
        }

        task publishAndRelease {
            group = "release"
            description = "First calls the publish task and then the io.codearte.nexus-staging's closeAndRelease task"

            dependsOn = [ "publish" ]
            finalizedBy = [ ":closeAndReleaseRepository" ]
        }
    }
}